buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.liquibase:liquibase-gradle-plugin:2.2.1"
    }
}

plugins {
    id 'org.springframework.boot' version '2.5.12'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'checkstyle'
    id 'io.freefair.lombok' version '6.2.0'
    id 'com.github.spotbugs' version '6.0.7'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'checkstyle'
apply plugin: 'com.github.spotbugs'
apply plugin: 'org.liquibase.gradle'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':common')
    implementation 'org.postgresql:postgresql:42.7.1'
    implementation 'org.jdbi:jdbi3-core:3.24.1'
    implementation 'org.jdbi:jdbi3-sqlobject:3.24.1'
    implementation 'org.jdbi:jdbi3-postgres:3.24.1'

    liquibaseRuntime 'org.liquibase:liquibase-core:4.6.1'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:3.0.3'
    liquibaseRuntime 'info.picocli:picocli:4.6.1'
    liquibaseRuntime 'org.postgresql:postgresql:42.7.1'
    liquibaseRuntime 'org.yaml:snakeyaml:1.17'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
}

test {
    useJUnitPlatform()
    testLogging {
        events "PASSED", "SKIPPED", "FAILED"
    }
}

sourceSets {
    main {
        resources {
            srcDirs += [ "src/main/java" ]
        }
    }
}

bootJar {
    enabled = false
}


tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    excludeFilter = rootProject.file('config/spotbugs/exclude.xml')
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

liquibase {
    activities {
        setup {
            changeLogFile 'changelog/setup/db.changelog-setup.xml'
            url System.getenv('PS_DB_URL') + '/postgres'
            username System.getenv('PS_DB_OWNER_NAME')
            password System.getenv('POSTGRES_PASSWORD')
        }

        main {
            changeLogFile 'changelog/db.changelog-master.xml'
            url System.getenv('PS_DB_URL') + '/patient_switching'
            username System.getenv('PS_DB_OWNER_NAME')
            password System.getenv('POSTGRES_PASSWORD')
        }

        runList = 'setup, main'
    }
}